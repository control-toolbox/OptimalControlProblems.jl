"""
Goddard Rocket Problem:
    We want to find the optimal trajectory of a Goddard rocket.
    The objective is to maximize the final altitude of the rocket.
    The problem is formulated as a JuMP model, and can be found [here](https://github.com/MadNLP/COPSBenchmark.jl/blob/main/src/rocket.jl)
"""
function OptimalControlProblems.rocket(::JuMPBackend; nh::Int64=100)
    h_0 = 1.0
    v_0 = 0.0
    m_0 = 1.0
    g_0 = 1.0
    T_c = 3.5
    h_c = 500.0
    v_c = 620.0
    m_c = 0.6

    c = 0.5 * sqrt(g_0 * h_0)
    m_f = m_c * m_0
    D_c = 0.5 * v_c * (m_0 / g_0)
    T_max = T_c * m_0 * g_0

    model = JuMP.Model()

    @variables(
        model,
        begin
            1.0 <= h[i = 0:nh], (start = 1.0)
            0.0 <= v[i = 0:nh], (start = i / nh * (1.0 - i / nh))
            m_f <= m[i = 0:nh] <= m_0, (start = (m_f - m_0) * (i / nh) + m_0)
            0.0 <= T[i = 0:nh] <= T_max, (start = T_max / 2.0)
            0.0 <= step, (start = 1 / nh)
        end
    )

    @expressions(
        model,
        begin
            D[i = 0:nh], D_c * v[i]^2 * exp(-h_c * (h[i] - h_0)) / h_0
            g[i = 0:nh], g_0 * (h_0 / h[i])^2
            dh[i = 0:nh], v[i]
            dv[i = 0:nh], (T[i] - D[i] - m[i] * g[i]) / m[i]
            dm[i = 0:nh], -T[i] / c
        end
    )

    @objective(model, Max, h[nh])

    # Dynamics
    @constraints(
        model,
        begin
            con_dh[i = 1:nh], h[i] == h[i - 1] + 0.5 * step * (dh[i] + dh[i - 1])
            con_dv[i = 1:nh], v[i] == v[i - 1] + 0.5 * step * (dv[i] + dv[i - 1])
            con_dm[i = 1:nh], m[i] == m[i - 1] + 0.5 * step * (dm[i] + dm[i - 1])
        end
    )

    # Boundary constraints
    @constraints(
        model,
        begin
            h_ic, h[0] == h_0
            v_ic, v[0] == v_0
            m_ic, m[0] == m_0
            m_fc, m[nh] == m_f
        end
    )

    return model
end
